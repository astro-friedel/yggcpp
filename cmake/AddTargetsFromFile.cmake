function(generate_target_file target_file)
  set(oneValueArgs OUTPUT_VAR DIRECTORY CUSTOM_TARGET)
  set(multiValueArgs TARGETS EXTRA_LIBRARIES EXTRA_DIRECTORIES)
  cmake_parse_arguments(ARGS "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})
  if (ARGS_DIRECTORY)
    cmake_path(APPEND target_file ${ARGS_DIRECTORY} ${target_file})
  endif()
  if(ARGS_TARGETS OR ARGS_EXTRA_LIBRARIES OR ARGS_EXTRA_DIRECTORIES)
    message(STATUS "targets = ${ARGS_TARGETS}")
    message(STATUS "target_file = ${target_file}")
    if(ARGS_TARGETS)
      file(GENERATE OUTPUT "${target_file}.$<CONFIG>"
           CONTENT "LIBRARIES;${ARGS_EXTRA_LIBRARIES};DIRECTORIES;${ARGS_EXTRA_DIRECTORIES};LIBRARIES;${ARGS_TARGETS};DIRECTORIES;$<TARGET_FILE_DIR:${ARGS_TARGETS}>")
    else()
      file(GENERATE OUTPUT "${target_file}.$<CONFIG>"
           CONTENT "LIBRARIES;${ARGS_EXTRA_LIBRARIES};DIRECTORIES;${ARGS_EXTRA_DIRECTORIES}")
    endif()
    add_custom_command(
        COMMAND ${CMAKE_COMMAND} "-E" "copy_if_different" "${target_file}.$<CONFIG>" "${target_file}"
	VERBATIM
	PRE_BUILD
	DEPENDS  "${target_file}.$<CONFIG>"
	OUTPUT   "${target_file}"
	COMMENT  "creating ${target_file} file ({event: PRE_BUILD}, {filename: ${target_file}})")
  else()
    set(target_file)
  endif()
  if (ARGS_OUTPUT_VAR)
    set(${ARGS_OUTPUT_VAR} ${target_file} PARENT_SCOPE)
  endif()
  if (ARGS_CUSTOM_TARGET)
    add_custom_target("${ARGS_CUSTOM_TARGET}" DEPENDS ${target_file})
  endif()
endfunction()

function(target_link_from_file target scope)
  set(multiValueArgs FILES)
  cmake_parse_arguments(ARGS "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGN})
  if (ARGS_FILES)
    set(files ${ARGS_FILES})
  else()
    set(files ${ARGS_UNPARSED_ARGUMENTS})
  endif()
  set(LIBS)
  set(DIRS)
  foreach(src IN LISTS files)
    message(STATUS "Begin file ${src}")
    set(curr)
    set(lines)
    if(EXISTS ${src})
      file(READ ${src} lines)
    endif()
    foreach(x IN LISTS lines)
      message(STATUS "  [${curr}] line ${x}")
      if(x STREQUAL "LIBRARIES")
        set(curr LIBS)
      elseif(x STREQUAL "DIRECTORIES")
        set(curr DIRS)
      elseif(NOT curr)
        message(FATAL_ERROR "Type for current line not set")
      elseif(x)
        list(APPEND ${curr} ${x})
      endif()
    endforeach()
  endforeach()
  if(LIBS)
    message(STATUS "Linking libraries ${LIBS}")
    target_link_libraries(${target} ${scope} ${LIBS})
  endif()
  if(DIRS)
     message(STATUS "Linking directories ${DIRS}")
     target_link_directories(${target} ${scope} ${DIRS})
  endif()
endfunction()
